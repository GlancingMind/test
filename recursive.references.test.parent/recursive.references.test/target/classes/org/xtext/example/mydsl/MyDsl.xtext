grammar org.xtext.example.mydsl.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/example/mydsl/MyDsl"

Model:
	(nodes+=NodeDefinition
	| systems+=SystemDefinition
	| deployments+=Deployment)*;



NodeDefinition: 'node' name=ID ';';
NodeInstance: type=[NodeDefinition] name=ID;

SystemDefinition:
	'system' name=ID '{'(
			'nodes' ':' (nodes+=NodeInstance ';')*
		| 'subsystems' ':' (subsystems+=SystemInstance ';')*
		)*
	'}'
;

SystemInstance: type=[SystemDefinition] name=ID;

SubSystemReference returns SystemReference:
	SystemReference ({SubSystemReference.parent=current} '.' system=[SystemInstance])*
;

SystemReference returns SystemReference:
	{SystemReference} system=[SystemInstance]
;

DeploymentStatement:
	'deploys' system=SubSystemReference '->' node=[NodeInstance] 'on' platform=[PlatformInstance] ';'
;

// TODO test function reference mapping

Deployment: {Deployment} 'deployment' '{'
		('platform' platforms+=PlatformInstance ';')*
		('system' systems+=SystemInstance ';')*
		(deployments+=DeploymentStatement)*
	'}'
;

PlatformInstance: name=ID;



