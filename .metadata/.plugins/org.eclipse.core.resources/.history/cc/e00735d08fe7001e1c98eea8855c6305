package org.xtext.fetaml.cpp.pkg

import org.xtext.fetaml.generator.PackageFile
import org.eclipse.xtend.lib.annotations.EqualsHashCode

@EqualsHashCode
package class ROSPackageManifest extends PackageFile {
	
	val ROSPackage pkg
	
	package new(ROSPackage pkg) {
		super(pkg, "package.xml")
		this.pkg = pkg
	}
	
	protected override getContent() '''
		<?xml version="1.0"?>
		<?xml-model href="http://download.ros.org/schema/package_format3.xsd" schematypens="http://www.w3.org/2001/XMLSchema"?>
		<package format="3">
			<name>«pkg.name»</name>
			<version>«pkg.version»</version>
			<description>«pkg.description»</description>
			«FOR maintainer : pkg.maintainers»
				<maintainer email="«maintainer.mail»">«maintainer.name»</maintainer>
			«ENDFOR»
			«FOR license : pkg.licenses»
				<license>«license»</license>
			«ENDFOR»
			«IF pkg.exportsMessages || pkg.exportsServices»
				<build_depend>rosidl_default_generators</build_depend>
			«ENDIF»
			
			<buildtool_depend>ament_cmake</buildtool_depend>
			
			<exec_depend>rclcpp</exec_depend>
			«IF pkg.exportsMessages || pkg.exportsServices»
				<exec_depend>rosidl_default_runtime</exec_depend>
			«ENDIF»
			«IF pkg.providesLaunchFiles»
				<exec_depend>ros2launch</exec_depend>
			«ENDIF»
			«IF pkg.requiresTopicTools»
				<exec_depend>topic_tools</exec_depend>
			«ENDIF»
			«IF pkg.hasDependenciesToOtherPackages»
		
				«FOR dependency : pkg.referencedPackages»
					<depend>«dependency.name»</depend>
				«ENDFOR»
			«ENDIF»
			
			<test_depend>ament_lint_auto</test_depend>
			<test_depend>ament_lint_common</test_depend>
			
			«IF pkg.exportsMessages || pkg.exportsServices»
				<member_of_group>rosidl_interface_packages</member_of_group>
			«ENDIF»
			
			<export>
				<build_type>ament_cmake</build_type>
			</export>
		</package>
	'''
}